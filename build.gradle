
buildscript {
    ext {
        swagger = "2.3.1"
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("io.swagger:swagger-codegen:${swagger}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

apply plugin: 'checkstyle'
apply plugin: 'findbugs'

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile('config/findbugs/plain.xsl')
    }
}

group 'jadecli'
version '0.1'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    generatedCompile
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

// Exclude the Spring logger, so everything will use SLF4J
configurations.all {
    exclude group: "commons-logging", module: "commons-logging"
}

dependencies {
    ext {
        commonsLang3 = "3.9"
        googleApiServicesOauth2 = "v2-rev20190313-1.30.1"
        googleClient = "1.30.3"
        googleCloudStorage = "1.92.0"
        googleOauthClientJetty = "1.30.2"
        jackson = "2.10.0.pr3"
        jersey = "2.29.1"
        junit = "4.12"
        swagger = "1.5.22"
        hamcrest = "2.2"
    }

    generatedCompile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "${jackson}"
    generatedCompile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${jackson}"
    generatedCompile group: 'io.swagger', name: 'swagger-annotations', version: "${swagger}"
    generatedCompile 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    generatedCompile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: "${jersey}"
    generatedCompile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: "${jersey}"
    generatedCompile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: "${jersey}"
    generatedCompile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jackson}"

    compile "org.apache.commons:commons-lang3:${commonsLang3}"
    compile 'ch.qos.logback:logback-classic:1.2.3'       // Logger
    compile 'org.slf4j:slf4j-api:1.7.28'                 // Logging facade

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${jackson}"
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jackson}"

    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: "${jersey}"
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: "${jersey}"
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: "${jersey}"
    compile "org.glassfish.jersey.inject:jersey-hk2:${jersey}"

    compile group: 'com.google.apis', name: 'google-api-services-oauth2', version: "${googleApiServicesOauth2}"
    compile group: 'com.google.api-client', name: 'google-api-client', version: "${googleClient}"
    compile group: 'com.google.api-client', name: 'google-api-client-java6', version: "${googleClient}"
    compile "com.google.cloud:google-cloud-storage:${googleCloudStorage}"
    compile group: 'com.google.oauth-client', name: 'google-oauth-client-jetty', version: "${googleOauthClientJetty}"

    // Findbugs annotations, so we can selectively suppress findbugs findings
    compileOnly 'com.google.code.findbugs:annotations:3.0.1u2'

    testCompile group: 'junit', name: 'junit', version: "${junit}"
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: "${hamcrest}"
}

test {
    testLogging {
        showStandardStreams = true // set to true to disable stdout & stderr redirection
    }
    // TODO: uncomment this once we have a solution for Travis running integration tests that require authentication
    exclude '**/CLICommandTests.class'
}

// -- Swagger Client Generation --
import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator
def openapiSourceFile = 'src/main/resources/data-repository-openapi.yaml'
def openapiTargetFolder = 'src/generated/java'

task generateApi {
    // specify the task inputs and outputs so gradle knows what to rebuild based on what has changed
    inputs.file("$projectDir/$openapiSourceFile")
    outputs.dir("$projectDir/$openapiTargetFolder")
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec("file:///$projectDir/$openapiSourceFile")
        config.setOutputDir("$projectDir")
        config.setLang('java')
        // These properties are not well documented. The only way I've found to figure them out is to
        // locally install the swagger-codegen tool and invoke it to get the specific help for the
        // spring boot options.
        config.setAdditionalProperties([
                'interfaceOnly' : 'true',                  // only generate the model and API interfaces
                'library'       : 'jersey2',               // do jersey code generation
                'dateLibrary'   : 'java8',                 // do java8 code generation
                'apiPackage'    : 'bio.terra.datarepo.api',
                'modelPackage'  : 'bio.terra.datarepo.model',
                'invokerPackage': 'bio.terra.datarepo.client',
                'sourceFolder'  : openapiTargetFolder
        ])
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

clean.doFirst {
    delete("${projectDir}/$openapiTargetFolder")
}
// -- End of Swagger Client Generation --


task testCLIUnit(type: Test) {
    useJUnit {
        includeCategories 'bio.terra.common.category.CLIUnit'
    }
    outputs.upToDateWhen { false }
}

task testCLIIntegrated(type: Test) {
    useJUnit {
        includeCategories 'bio.terra.common.category.CLIIntegrated'
    }
    outputs.upToDateWhen { false }
}

task checkApiUpToDate(type: Exec) {
    commandLine 'sh'
    args './checkApiUpToDate.sh'
}

sourceSets {
    generated {
        compileClasspath = configurations.generatedCompile
    }
    main {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
    test {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
    checkstyle {
        toolVersion '7.8.1'
        checkstyleGenerated.enabled = false // Don't check generated code
        configFile file("config/checkstyle/checkstyle.xml")
    }
    findbugs {
        toolVersion '3.0.1'
        ignoreFailures = false
        excludeFilter = file("config/findbugs/excludeFilter.xml")
        reportsDir = file("$project.buildDir/reports/findbugs")
        effort = "max"
    }
}
mainClassName = 'bio.terra.Main'

jar {
    from sourceSets.generated.output
}

compileGeneratedJava.dependsOn generateApi
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava
ideaModule.dependsOn generateApi
